Programa	::=	Class* DecVar* Function* Main

Class		::=	'class' 'id' ('hamon' 'id')? BodyClass

BodyClass	::=	'{' DecVarClass* FunctionClass* '}'

DecVarClass	::=	Access DecVar

FunctionClass	::= 'func' Access (Type | 'stand' | 'id') 'id' Parameters Body

Function	::= 'func' (Type | 'stand' | 'id') 'id' Parameters Body

Parameters	::= '(' ('ref'? (Type | 'id') 'id') (',' 'ref'? (Type | 'id') 'id')* ')'

Main		::= 'public' 'stand' 'jojo' '(' ')' Body

Body		::= '{' DecVar* Action* '}'

Action		::= Assign
			|	Input
			|	Output
			|	Condition
			|	While
			|	Funcall
			|	Return

DecVar		::= Type DecVar2 (',' DecVar2)* ';'
DecVar2		::= 'id' ('[' cte_int ']')? ('.' 'id' ('[' cte_int ']')?)*

Var         ::= 'id' (
                    |   ( '(' Expression (',' Expression)* ')' )
                    |   ( ('[' Expression ']') ('.' Var)? )
                    |   ( '.' Var))


Var2        ::= 'id' (
                    |   ('[' Expression ']' ('.' Var2)? )
                    |   ( '.' Var2) )


Funcall		::= 'id' ('[' Expression ']')? ('.' 'id'  ('[' Expression ']')? )* '(' Expression (',' Expression)* ')' ';'

Assign		::=	Var '=' (Expression | 'new' 'id' '(' ')') ';'

Input		::= 'gets' '(' Var ')' ';'

Output		::= 'prints' '(' Expression ')' ';'

Condition	::= 'if' '(' Expression ')' '{' Action* '}' ('else' 'if' '(' Expression ')' '{' Action* '}')* ('else' '{' Action* '}')?

While		::= 'while' '(' Expression ')' '{' Action* '}'

Return		::= 'zadust' Expression? ';'

Expression 			::= 'ExpressionNot' (('<' | '>' | '==' | '<=' | '>=' | '!=') 'ExpressionNot')?

ExpressionNot		::= '!'* ExpressionOr

ExpressionOr 		::= ExpressionAnd ('||' ExpressionAnd)*

ExpressionAnd		::= ExpressionAS ('&&' ExpressionAS)*

ExpressionAS 		::= ExpressionMD (('+'|'-') ExpressionMD)*

ExpressionMD 		::= ExpressionL (('*' | '/' | '%') ExpressionL)*

ExpressionL 		::= '(' Expression ')' 
					| ('+'|'-')? (Var | Funcall | Const)

Const				::= 'cte_int'
					|	'cte_real'
					|	'cte_string'
					|	'cte_bool'


Access		::= 'public'
			|	'private'
			|	


Type		::=	'int'
			|	'real'
			|	'bool'
			|	'string'